<file> ::= <statement> 
 
<statement> ::= <statement> <statement>
	  | "{" <statement> "}"
	  | "while" "(" <expression> ")" "{" <statement> "}"
	  | "if" "(" <expression> ")" "{" <statement> "}"
	  | "if" "(" <expression> ")" "{" <statement> "}" "else" "{" <statement> "}"
	  | "repeat" "(" <expression> ")" "{" <statement> "}"
	  | <definition> ";"
	  | <expression> ";"
	  | <assignment> ";"
	  | "pause" <reint> ";"
	  | "outstring" <restring> ";"
	  | "outint" <reint> ";"
	  | "lclick" ";"
	  | "rclick" ";"
	  | "lrelease" ";"
	  | "rrelease" ";"
	  | "up" <reint> ";"
	  | "down" <reint> ";"
	  | "left" <reint> ";"
	  | "right" <reint> ";"
	  | "presskey" <restring> ";"
	  | "releasekey" <restring> ";" 
 
<definition> ::= <type> "id"
	   | <type> <assignment> 
 
<assignment> ::= "id" "=" <reint>
	   | "id" "=" <fuint>
	   | "id" "=" <restring>
	   | "id" "=" <fustring> 
 
<type> ::= "int"
     | "string"
 
<expression> ::= <conjunction>
	   | <expression> "or" <conjunction> 
 
<conjunction> ::= <equality>
	    | <conjunction> "and" <equality> 
 
<equality> ::= <relation>
	 | <relation> <equop> <relation> 
 
<equop> ::= "=="
      | "!=" 
 
<relation> ::= <addition>
	 | <addition> <relop> <addition> 

<relop> ::= "<"
      | "<="
      | ">"
      | ">=" 

<addition> ::= <term>
	 | <addition> <addop> <term> 

<addop> ::= "+"
      | "-" 

<term> ::= <factor>
     | <term> <mulop> <factor> 

<mulop> ::= "*"
      | "/"
      | "%" 

<factor> ::= <unaryop> <primary>
       | <primary> 
 
<unaryop> ::= "-"
		| "!" 
		
<primary> ::= "id"
        | "int"
        | "(" <expression> ")" 

<fuint> ::= "inint"
      | "mouseposx"
      | "mouseposy" 

<fustring> ::= "instring" 

<reint> ::= <expression>

<restring> ::= "string"
	 | "id" 
